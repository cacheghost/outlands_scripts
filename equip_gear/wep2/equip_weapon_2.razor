# If we havent set our weapon, set it.
if not varexist 'weapon_2'
    overhead '[Select Weapon 2]' 
    setvar! 'weapon_2'
endif

# Check if we are casting
if varexist 'weapon_2'
    # Look for weapon to equip
    if find 'weapon_2' 'backpack'
        # Weapon found in backpack.
        if findlayer self lefthand as 'myweapon'
            if 'myweapon' != 'weapon_2'
                # Weapon we are wearing isnt our weapon, change that.
                overhead '[Equip Weapon 2]' 55
                lift 'myweapon'
                drop 'backpack'
                lift 'weapon_2'
                drop self LeftHand
            endif
        elseif findlayer self righthand as 'myweapon'
            if 'myweapon' != 'weapon_2'
                # Weapon we are wearing isnt our weapon, change that.
                overhead '[Equip Weapon 2]' 55
                lift 'myweapon'
                drop 'backpack'
                lift 'weapon_2'
                drop self RightHand
            endif
        else
            # Not wearing weapon, equip from backpack
            overhead '[Equip Weapon 2]' 55
            lift 'weapon_2'
            drop self LeftHand
        endif
    else
        # We are not wearing the weapon, and its not in backpack. Unset variable
        if findlayer self lefthand as 'myweapon'
            if 'myweapon' != 'weapon_2'
                overhead '[Unset Weapon 2]' 33
                @unsetvar 'weapon_2'
            endif
        elseif findlayer self righthand as 'myweapon'
            if 'myweapon' != 'weapon_2'
                overhead '[Unset Weapon 2]' 33
                @unsetvar 'weapon_2'
            endif
        else
            overhead '[Unset Weapon 2]' 33
            @unsetvar 'weapon_2'
        endif
    endif
endif
script main