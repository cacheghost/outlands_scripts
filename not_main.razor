#overhead '[M]'
####################################################
#
#   Script: Main (Exosuit)
#   Author: CacheGhost
#   
#   Sections:
#   1. Overhead Messages and Global Timers
#   2. Explosion Potion Timer
#   3. Swing Timers
#   4. Bandage Routine
#   5. YOU ARE DEAD!
#
####################################################
while not dead
########################################################
    # Section 1: Overhead Messages and Global Timers
    #--------------------------------------------------#
    # Free Cure Potion Alert
    #--------------------------------------------------#
    if not timerexists 'next free cure'
        createtimer 'next free cure'
        settimer 'next free cure' 60001
    endif
    if timer 'next free cure' > 30000
        if timer 'next free cure' > 60000
            # Leave this comment blank, this is needed.
        else
            overhead '[Free Cure Ready]'
            settimer 'next free cure' 60001
        endif
    endif
    #--------------------------------------------------#
    # World Save timer and warning
    #--------------------------------------------------#
    if insysmsg 'world will save in 15 seconds'
        overhead '[World Save Warning]' 55
    endif
    if insysmsg 'world is saving, please wait'
        overhead '[World Save]' 33
    endif
    #--------------------------------------------------#
    # Spell Detection
    #--------------------------------------------------#
    if insysmsg 'MagicResist skillgain'
        overhead '[Spell Attack]' 33
    endif
    #--------------------------------------------------#
    # PVP Event Messages
    #--------------------------------------------------#
    # Kills and Kill Points
    #--------------------------------------------------#
    if insysmsg 'You have been credited with'
        overhead '[Kill Earned]' 33
    endif
    if insysmsg 'kill points earned'
        overhead '[KP Earned]' 423
    endif
    #--------------------------------------------------#
    # Control Ticks
    #--------------------------------------------------#
    if not timerexists 'cp timer'
        createtimer 'cp timer'
        settimer 'cp timer' 30000
    endif
    if insysmsg '10 control points earned'
        overhead '[10 CP Earned]' 423
        settimer 'cp timer' 0
    elseif insysmsg '15 control points earned'
        overhead '[15 CP Earned]' 423
        settimer 'cp timer' 0
    elseif insysmsg '20 control points earned'
        overhead '[20 CP Earned]' 423
        settimer 'cp timer' 0
    endif
    if not timerexists 'cp spam'
        createtimer 'cp spam'
        settimer 'cp spam' 5000
    endif
    if timer 'cp timer' >= 31000
        # Leave this comment blank, this is needed.
    elseif timer 'cp timer' >= 30000 and timer 'cp spam' >= 5000
        overhead '[CP Tick Now]'
        settimer 'cp spam' 0
    elseif timer 'cp timer' >= 25000 and timer 'cp spam' >= 5000
        overhead '[CP Tick 5 Sec]'
        settimer 'cp spam' 0
    elseif timer 'cp timer' >= 20000 and timer 'cp spam' >= 5000
        overhead '[CP Tick 10 Sec]'
        settimer 'cp spam' 0
    elseif timer 'cp timer' >= 15000 and timer 'cp spam' >= 5000
        overhead '[CP Tick 15 Sec]'
        settimer 'cp spam' 0
    elseif timer 'cp timer' >= 10000 and timer 'cp spam' >= 5000
        overhead '[CP Tick 20 Sec]'
        settimer 'cp spam' 0
    elseif timer 'cp timer' >= 5000 and timer 'cp spam' >= 5000
        overhead '[CP Tick 25 Sec]'
        settimer 'cp spam' 0
    endif
    #--------------------------------------------------#
    # Hamstring
    #--------------------------------------------------#
    if insysmsg 'will now attempt to hamstring'
        overhead '[Hamstring: On]' 68
    elseif insysmsg 'refrain from making hamstring'
        overhead '[Hamstring: Off]' 33
    elseif insysmsg 'fail to hamstring'
        overhead '[Hamstring Fail]' 33
    elseif insysmsg 'your attack hamstrings your target'
        overhead '[Hamstrung Target]'
    endif
    #--------------------------------------------------#
    # Disarm
    #--------------------------------------------------#
    if insysmsg 'will now attempt to disarm'
        overhead '[Disarm: On]' 68
    elseif insysmsg 'refrain from making disarm'
        overhead '[Disarm: Off]' 33
    elseif insysmsg 'fail to disarm'
        overhead '[Disarm Fail]' 33
    endif
    #--------------------------------------------------#
    # Prevent Criminal Looting
    #--------------------------------------------------#
    if insysmsg 'Criminal looting will now be allowed'
        overhead '[Allow Crim Loot]' 68
    elseif insysmsg 'Criminal looting will now be prevented'
        overhead '[Prevent Crim Loot]' 33
    endif
    #--------------------------------------------------#
    # Prevent Criminal Healing
    #--------------------------------------------------#
    if insysmsg 'Criminal healing will now be allowed'
        overhead '[Allow Crim Healing]' 68
    elseif insysmsg 'Criminal healing will now be prevented'
        overhead '[Prevent Crim Healing]' 33
    endif
    #--------------------------------------------------#
    # Aspect returns
    #--------------------------------------------------#
    if insysmsg 'Your aspect and other bonuses return'
        overhead '[Aspect Returned]' 483
    endif
    #--------------------------------------------------#
    # Ate mushroom
    #--------------------------------------------------#
    if insysmsg 'You consume a magic mushroom'
        overhead '[Ate Mushroom]' 68
    endif
    #--------------------------------------------------#
    # Able to eat mushroom
    #--------------------------------------------------#
    if insysmsg 'You may now use another magic mushroom'
        overhead '[Mushroom Ready]' 483
    endif
    #--------------------------------------------------#
    # Interrupt
    #--------------------------------------------------#
    if insysmsg 'concentration is disturbed'
        overhead '[Interrupted]' 125
    endif
    #--------------------------------------------------#
    # Already Casting
    #--------------------------------------------------#
    if insysmsg 'You are already casting'
        overhead '[Already Casting]' 88
    endif
    #--------------------------------------------------#
    # Ranover
    #--------------------------------------------------#
    if insysmsg 'you shove them out of the way'
        overhead '[Ranover Somebody]' 44
    endif
    #--------------------------------------------------#
    # Ranover Invis
    #--------------------------------------------------#
    if insysmsg 'you shove something invisible'
        overhead '[Ranover Invis]' 44
    endif
    #--------------------------------------------------#
    # Resist Poison
    #--------------------------------------------------#
    if insysmsg 'You resist a poison effect'
        overhead '[Resist Poison]' 68
    endif
    #--------------------------------------------------#
    # Resist Disease
    #--------------------------------------------------#
    if insysmsg 'You resist a disease effect'
        overhead '[Resist Disease]' 68
    endif
    #--------------------------------------------------#
    # Resist Bleed
    #--------------------------------------------------#
    if insysmsg 'You resist a bleed effect'
        overhead '[Resist Bleed]' 68
        clearsysmsg
    endif
    #--------------------------------------------------#
    # Stealth Refresh
    #--------------------------------------------------#
    if insysmsg 'ready to continue stealthing'
        overhead '[Stealth Refresh]' 68
    endif
    #--------------------------------------------------#
    # Hinder Target
    #--------------------------------------------------#
    if insysmsg 'hinders your target'
        overhead '[Hindered Target]' 68
    endif
    #--------------------------------------------------#
    # Mana Refund
    #--------------------------------------------------#
    if insysmsg 'spellbook generates mana'
        overhead '[Mana Refund]' 125
    endif
    #--------------------------------------------------#
    # Charged Spell
    #--------------------------------------------------#
    if insysmsg 'charge your spell'
        overhead '[Charged Spell]' 125
    endif
    #--------------------------------------------------#
    # Wizardry Hinder Ready
    #--------------------------------------------------#
    if insysmsg 'cast a wizardry lightning spell again'
        overhead '[Lightning Hinder Ready]' 44
    endif
    #--------------------------------------------------#
    # Meditating
    #--------------------------------------------------#
    if insysmsg 'enter a meditative trance'
        overhead '[Meditating]' 506
    endif
    #--------------------------------------------------#
    # Stop Meditating
    #--------------------------------------------------#
    if insysmsg 'stop meditating'
        overhead '[Stop Meditating]' 33
    endif
    #--------------------------------------------------#
    # Monitor for free cure pot
    #--------------------------------------------------#
    if insysmsg 'receive a free cure potion'
        settimer 'next free cure' 0
    endif
########################################################

########################################################
    # Section 3: Explosion Pot Timer
    #--------------------------------------------------#
    if not timerexists 'explo_pot_spam'
        createtimer 'explo_pot_spam'
        settimer 'explo_pot_spam' 20000
    endif
    if not timerexists 'explo_pot'
        createtimer 'explo_pot'
        settimer 'explo_pot' 36000
    endif
    if insysmsg 'next explosion potion thrown at that player'
        settimer 'explo_pot' 0
    endif
    if timerexists 'explo_pot'
        if timer 'explo_pot' >= 35000 and timer 'explo_pot_spam' > 5000
            # Leave this comment blank, this is needed.
        elseif timer 'explo_pot' >= 30000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot Ready]' 68
            settimer 'explo_pot_spam' 0
        elseif timer 'explo_pot' >= 25000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot - 5 sec]' 55
            settimer 'explo_pot_spam' 0
        elseif timer 'explo_pot' >= 20000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot - 10 sec]' 55
            settimer 'explo_pot_spam' 0
        elseif timer 'explo_pot' >= 15000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot - 15 sec]' 55
            settimer 'explo_pot_spam' 0
        elseif timer 'explo_pot' >= 10000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot - 20 sec]' 55
            settimer 'explo_pot_spam' 0
        elseif timer 'explo_pot' >= 0 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot - 30 sec]' 55
            settimer 'explo_pot_spam' 0
        endif
    endif
########################################################
    
########################################################
    # Section 4: Swing Timers
    #
    #   You need to have less than GM Arms Lore.
    #   Additionally, you need to be max skills so you dont actually gain.
    #   Uptick Arms Lore and turn on Skill Gain Chance announcements.
    #--------------------------------------------------#
    if not timerexists 'swing timer'
        createtimer 'swing timer'
        settimer 'swing timer' 20000
    endif
    if insysmsg 'ArmsLore skillgain:'
        settimer 'swing timer' 0
    endif
    #--------------------------------------------------#
    # Add a weapon spam timer here to avoid overhead spam.
    #--------------------------------------------------#
    if timerexists 'swing timer'
        #--------------------------------------------------#
        # Bow Overhead Spam
        #--------------------------------------------------#
        if not timerexists 'bow spam'
            createtimer 'bow spam'
            settimer 'bow spam' 20000
        endif
        #--------------------------------------------------#
        # Crossbow Overhead Spam
        #--------------------------------------------------#
        if not timerexists 'xbow spam'
            createtimer 'xbow spam'
            settimer 'xbow spam' 20000
        endif
        #--------------------------------------------------#
        # Hally Overhead Spam
        #--------------------------------------------------#
        if not timerexists 'hally spam'
            createtimer 'hally spam'
            settimer 'hally spam' 20000
        endif
        #--------------------------------------------------#
        # Heavy Crossbow Overhead Spam
        #--------------------------------------------------#
        if not timerexists 'heavy xbow spam'
            createtimer 'heavy xbow spam'
            settimer 'heavy xbow spam' 20000
        endif
        #--------------------------------------------------#
        # Swing timer check against swing speed of weapon.
        #--------------------------------------------------#
        if timer 'swing timer' >= 6500
            # Leave this comment blank, this is needed.
        #--------------------------------------------------#
        # Heavy Crossbow Swing Timer
        #--------------------------------------------------#
        elseif timer 'swing timer' >= 6000 and timer 'heavy xbow spam' > 5000
            overhead '[Heavy Xbow Ready]' 55
            settimer 'heavy xbow spam' 0
        #--------------------------------------------------#
        # Hally Swing Timer
        #--------------------------------------------------#
        elseif timer 'swing timer' >= 4800 and timer 'hally spam' > 5000
            #overhead '[Hally Ready]' 55
            settimer 'hally spam' 0
        #--------------------------------------------------#
        # Crossbow Swing Timer
        #--------------------------------------------------#
        elseif timer 'swing timer' >= 4300 and timer 'xbow spam' > 5000
            #overhead '[Xbow Ready]' 55
            settimer 'xbow spam' 0
        #--------------------------------------------------#
        # Bow Swing Timer
        #--------------------------------------------------#
        elseif timer 'swing timer' >= 3520 and timer 'bow spam' > 5000
            #overhead '[Bow Ready]' 55
            settimer 'bow spam' 0
        endif    
    endif
########################################################
    
########################################################
    # Section 4: Bandage Survival
    #
    #   Dex = Time to Heal
    #   25 = 15
    #   40 = 14
    #   55 = 13
    #   70 = 12
    #   85 = 11
    #   100 = 10
    #--------------------------------------------------#
    if skill 'Healing' >= 1
        #--------------------------------------------------#
        # Initial timers
        #--------------------------------------------------#
        if not timerexists 'bandage time'
            createtimer 'bandage time'
            settimer 'bandage time' 20000
        endif
        if not timerexists 'aid cure sync'
            createtimer 'aid cure sync'
            settimer 'aid cure sync' 20000
        endif
        #--------------------------------------------------#
        # If we manually applied a bandage, reset timer.
        #--------------------------------------------------#
        if insysmsg 'You begin applying the bandages'
            settimer 'bandage time' 0
            clearsysmsg
        endif
        #--------------------------------------------------#
        # If check to do bandage routine based off dex stat
        #--------------------------------------------------#
        # Dex 0-40 (15 seconds)
        #--------------------------------------------------#
        if targetexists any or hidden
            # Do nothing if we are holding a spell or hidden
        else
            if dex <= 40
                # 15 second timer
                if hits < maxhits and timer 'bandage time' >= 15000         
                    hotkey 'bandage self'
                    settimer 'bandage time' 0
                endif
                # Attempt to last second cure before aid tick, regardless of free cut timer.
                if timer 'bandage time' >= 14000
                    if poisoned 'self' and timer 'aid cure sync' > 1000
                        overhead '[Used: Cure Pot]' 68
                        hotkey 'Drink Cure'
                        # Set timer for free cure
                        if insysmsg 'receive a free cure potion'
                            settimer 'next free cure' 0
                        endif
                        settimer 'aid cure sync' 0
                    endif
                endif
            #--------------------------------------------------#
            # Dex 41-54 (14 seconds)
            #--------------------------------------------------#    
            elseif dex < 55
                # 14 second timer
                if hits < maxhits and timer 'bandage time' >= 14000
                    hotkey 'bandage self'
                    settimer 'bandage time' 0
                endif
                # Attempt to last second cure before aid tick, regardless of free cut timer.
                if timer 'bandage time' >= 13000
                    if poisoned 'self' and timer 'aid cure sync' > 1000
                        overhead '[Used: Cure Pot]' 68
                        hotkey 'Drink Cure'
                        # Set timer for free cure
                        if insysmsg 'receive a free cure potion'
                            settimer 'next free cure' 0
                        endif
                        settimer 'aid cure sync' 0
                    endif
                endif
            #--------------------------------------------------#
            # Dex 55-69 (13 seconds)
            #--------------------------------------------------#
            elseif dex < 70
                # 13 second timer
                if hits < maxhits and timer 'bandage time' >= 13000 
                    hotkey 'bandage self'
                    settimer 'bandage time' 0
                endif
                # Attempt to last second cure before aid tick, regardless of free cut timer.
                if timer 'bandage time' >= 12000
                    if poisoned 'self' and timer 'aid cure sync' > 1000
                        overhead '[Used: Cure Pot]' 68
                        hotkey 'Drink Cure'
                        # Set timer for free cure
                        if insysmsg 'receive a free cure potion'
                            settimer 'next free cure' 0
                        endif
                        settimer 'aid cure sync' 0
                    endif
                endif
            #--------------------------------------------------#
            # Dex 70-84 (12 seconds)
            #--------------------------------------------------#
            elseif dex < 85
                # 12 second timer
                if hits < maxhits and timer 'bandage time' >= 12000 
                    hotkey 'bandage self'
                    settimer 'bandage time' 0
                endif
                # Attempt to last second cure before aid tick, regardless of free cut timer.
                if timer 'bandage time' >= 11000
                    if poisoned 'self' and timer 'aid cure sync' > 1000
                        overhead '[Used: Cure Pot]' 68
                        hotkey 'Drink Cure'
                        # Set timer for free cure
                        if insysmsg 'receive a free cure potion'
                            settimer 'next free cure' 0
                        endif
                        settimer 'aid cure sync' 0
                    endif
                endif
            #--------------------------------------------------#
            # Dex 85-99 (11 seconds)
            #--------------------------------------------------#
            elseif dex < 100
                # 11 second timer
                if hits < maxhits and timer 'bandage time' >= 11000 
                    hotkey 'bandage self'
                    settimer 'bandage time' 0
                endif
                # Attempt to last second cure before aid tick, regardless of free cut timer.
                if timer 'bandage time' >= 10000
                    if poisoned 'self' and timer 'aid cure sync' > 1000
                        overhead '[Used: Cure Pot]' 68
                        hotkey 'Drink Cure'
                        # Set timer for free cure
                        if insysmsg 'receive a free cure potion'
                            settimer 'next free cure' 0
                        endif
                        settimer 'aid cure sync' 0
                    endif
                endif
            #--------------------------------------------------#
            # Dex 100+ (10 seconds)
            #--------------------------------------------------#
            else
                # 10 second timer
                if hits < maxhits and timer 'bandage time' >= 10000
                    hotkey 'bandage self'
                    settimer 'bandage time' 0
                endif
                # Attempt to last second cure before aid tick, regardless of free cut timer.
                if timer 'bandage time' >= 9000
                    if poisoned 'self' and timer 'aid cure sync' > 1000
                        overhead '[Used: Cure Pot]' 68
                        hotkey 'Drink Cure'
                        # Set timer for free cure
                        if insysmsg 'receive a free cure potion'
                            settimer 'next free cure' 0
                        endif
                        settimer 'aid cure sync' 0
                    endif
                endif
            endif
        endif
    endif
########################################################

########################################################
endwhile

########################################################
# Section 5: YOU ARE DEAD!
#--------------------------------------------------#
if dead
    overhead 'You are dead! GJ NOOB!'
    stop
endif
########################################################