while not dead
    clearsysmsg
    if not timerexists "skill timer"
        createtimer "skill timer"
    endif

    if timer "skill timer" > 10100
        hotkey 'Cancel Current Target'
        skill 'detectinghidden'
        waitfortarget 5000
        target 'self'
        settimer "skill timer" 0
    endif
    unsetvar 'lastserial'
    hotkey 'Next Humanoid Target'
    pause 350
    if not insysmsg "No one matching that was found"
        setvar! 'lastserial' lasttarget
        getlabel 'lastserial' 'seriallabel'
        # If the target is green, skip.
        if noto 'lastserial' = friend
            overhead "Ignore Guildmate" 33
            continue
        endif
        
        if noto 'lastserial' = invulnerable
            overhead "Ignore NPC" 33
            continue
        endif
        
        # If the target is in *** guild, skip.
        if ", Rage]" in 'seriallabel' or "[Rage]" in 'seriallabel'
            overhead "Ignore Red Guildmate" 33
            continue
        endif
        
        # Attempt to ban the target.
        hotkey 'Cancel Current Target'
        say "i ban thee"
        pause 250
        target 'lastserial'
        pause 500
    endif
endwhile