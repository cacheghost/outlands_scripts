# Used in dueling since you cannot precast heal.
if targetexists 'beneficial'
    target self
endif

# If we havent set our weapon, set it.
if not varexist 'weapon_1'
    overhead '[Select Weapon 1]' 
    setvar! 'weapon_1'
endif

# Check if we are casting
if varexist 'weapon_1'
    # Ignore weapon equip if we are trying to cast.
    if timerexists 'cast recovery'
        if timer 'cast recovery' < 1500
            overhead '[Ignore Equip For Cast]' 33
            script main
        endif
    endif
    
    # Look for weapon to equip
    if find 'weapon_1' 'backpack'
        # Weapon found in backpack.
        if findlayer self lefthand as 'myweapon'
            if 'myweapon' != 'weapon_1'
                # Weapon we are wearing isnt our weapon, change that.
                overhead '[Equip Weapon 1]' 55
                lift 'myweapon'
                drop 'backpack'
                lift 'weapon_1'
                drop self LeftHand
            endif
        elseif findlayer self righthand as 'myweapon'
            if 'myweapon' != 'weapon_1'
                # Weapon we are wearing isnt our weapon, change that.
                overhead '[Equip Weapon 1]' 55
                lift 'myweapon'
                drop 'backpack'
                lift 'weapon_1'
                drop self RightHand
            endif
        else
            # Not wearing weapon, equip from backpack
            overhead '[Equip Weapon 1]' 55
            lift 'weapon_1'
            drop self LeftHand
        endif
    else
        # We are not wearing the weapon, and its not in backpack. Unset variable
        if findlayer self lefthand as 'myweapon'
            if 'myweapon' != 'weapon_1'
                overhead '[Unset Weapon 1]' 33
                @unsetvar 'weapon_1'
            endif
        elseif findlayer self righthand as 'myweapon'
            if 'myweapon' != 'weapon_1'
                overhead '[Unset Weapon 1]' 33
                @unsetvar 'weapon_1'
            endif
        endif
    endif
endif
script main