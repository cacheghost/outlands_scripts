# Clear sys messaages
clearsysmsg

# Stay Alive Starting message
overhead 'Stay Alive Starting!' 44

# Setup some timers
createtimer 'bandage time'
createtimer 'curepot recovery'
createtimer 'healpot recovery'
createtimer 'potion recovery'
createtimer 'castspell recovery'
createtimer 'stayalive text'
createtimer 'low mana'

# Start some timers
settimer 'bandage time' 20000
settimer 'curepot recovery' 20000
settimer 'castspell recovery' 20000
settimer 'healpot recovery' 20000
settimer 'potion recovery' 20000

# While alive, try to stay alive.
while not dead
    # Display script running every 10 seconds. 
    if timer 'stayalive text' > 10000
        overhead 'Stay Alive Running!' 66
        settimer 'stayalive text' 0
    endif

    # If hidden, skip
    if hidden
        continue
    endif
    
    # If paralyzed, attempt to pouch.
    if insysmsg 'you cannot move!'
        overhead 'Paralyzed, attempting pouch escape' 88
        say [pouch
    endif

    # Healing Survival
    if skill 'Healing' >= 50
        if insysmsg 'You begin applying the bandages'
            settimer 'bandage time' 0
            clearsysmsg
        endif
        if hits < maxhits and timer 'bandage time' > 11000 
            hotkey 'bandage self'
            settimer 'bandage time' 0
        endif
    endif

    # Magery Survival
    if skill 'Magery' >= 60
        # Mana warnings
        if mana < 22  and timer 'low mana' > 3000 and mana > 10
            overhead '1 GHeal Left' 55
            settimer 'low mana' 0
        elseif mana < 11  and timer 'low mana' > 3000
            overhead '0 GHeals Left' 33
            settimer 'low mana' 0
        endif

        # If hurt, attempt to heal
        if hits < 65 and timer 'castspell recovery' > 1500
            # If under 35 HP and we cant heal pot, quick heal and reset heal timer
            # Interrupt and clearall targets before attempting to self-cast to avoid harming self
            if hits < 35 and timer 'healpot recovery' < 10000 and timer 'potion recovery' < 1000 and mana > 3
                overhead 'Miniheal' 33
                interrupt    
                hotkey 'Cancel Current Target'
                hotkey 'Clear Target Queue'
                hotkey '> Mini-Heal/Cure Self'
                settimer 'castspell recovery' 0
            elseif hits < maxhits and mana > 10
                overhead 'Gheal' 33
                # Otherwise just use greater heal to heal up.
                interrupt
                hotkey 'Cancel Current Target'
                hotkey 'Clear Target Queue'
                hotkey '> Greater Heal/Cure Self'
                settimer 'castspell recovery' 0
            endif
        endif
    endif

    if not findbuff 'Magic Resist Potion' and timer 'potion recovery' > 1000
        overhead 'Used: MR Potion' 33
        hotkey 'Drink Night Sight'
        settimer 'potion recovery' 0
    endif
    
    # Cure Survival
    if poisoned 'self' and timer 'potion recovery' > 1000
        overhead 'Used: Cure Potion' 33
        hotkey 'Drink Cure' 
        settimer 'potion recovery' 0
        settimer 'curepot recovery' 0
    endif

    # Heal Potion
    if hits < 35 and timer 'healpot recovery' > 10000 and timer 'potion recovery' > 1000
        overhead 'Used: Heal Potion' 33
        hotkey 'Drink Heal'
        settimer 'healpot recovery' 0
        settimer 'potion recovery' 0
    endif 

endwhile

if dead
    overhead 'Stay Alive Failed!'
    stop
endif