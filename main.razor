#overhead '[M]'
# timer initialization
if not timerexists 'potion recovery'
    createtimer 'potion recovery'
    settimer 'potion recovery' 20000
endif

# Main Loop
while not dead
    ####################################################
    # Overhead Messages and Global Timers
    ####################################################
    # Display script running every 30 seconds. 
    if not timerexists 'stayalive text'
        createtimer 'stayalive text'
        overhead 'Stay Alive Starting!' 66
    endif
    
    if timer 'stayalive text' > 30000
        #overhead 'Stay Alive Running!' 66
        settimer 'stayalive text' 0
    endif
    
    # Free Cure Potion Alert
    if not timerexists 'next free cure'
        createtimer 'next free cure'
        settimer 'next free cure' 60001
    endif
    
    if timer 'next free cure' > 30000
        if timer 'next free cure' > 60000
            #
        else
            overhead '[Free Cure Ready]'
            settimer 'next free cure' 60001
        endif
    endif

    # World Save timer and warning
    if insysmsg 'world will save in 15 seconds'
        overhead '[World Save Warning]' 55
    endif
    
    if insysmsg 'world is saving, please wait'
        overhead '[World Save]' 33
    endif
    
    # Spell Detection
    if insysmsg 'MagicResist skillgain'
        overhead '[Spell Attack]' 33
    endif
    
    # Explosion Pot Timer
    if not timerexists 'explo_pot_spam'
        createtimer 'explo_pot_spam'
        settimer 'explo_pot_spam' 20000
    endif
    
    if insysmsg 'next explosion potion thrown at that player'
        settimer 'explo pot' 0
    endif
    
    if timerexists 'explo pot'
        if timer 'explo pot' >= 35000 and timer 'explo_pot_spam' > 5000
            #
        elseif timer 'explo pot' >= 30000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot Ready]' 68
            settimer 'explo_pot_spam' 0
        elseif timer 'explo pot' >= 25000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot - 5 sec]' 55
            settimer 'explo_pot_spam' 0
        elseif timer 'explo pot' >= 20000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot - 10 sec]' 55
            settimer 'explo_pot_spam' 0
        elseif timer 'explo pot' >= 15000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot - 15 sec]' 55
            settimer 'explo_pot_spam' 0
        elseif timer 'explo pot' >= 10000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot - 20 sec]' 55
            settimer 'explo_pot_spam' 0
        elseif timer 'explo pot' >= 5000 and timer 'explo_pot_spam' > 5000
            overhead '[Explo Pot - 25 sec]' 55
            settimer 'explo_pot_spam' 0
        endif
    endif
    
    # Swing Timers
    if not timerexists 'swing timer'
        createtimer 'swing timer'
        settimer 'swing timer' 20000
    endif
    if insysmsg 'ArmsLore skillgain:'
        settimer 'swing timer' 0
    endif
    
    if timerexists 'swing timer'
        if not timerexists 'bow spam'
            createtimer 'bow spam'
            settimer 'bow spam' 20000
        endif

        if not timerexists 'xbow spam'
            createtimer 'xbow spam'
            settimer 'xbow spam' 20000
        endif

        if not timerexists 'hally spam'
            createtimer 'hally spam'
            settimer 'hally spam' 20000
        endif
        
        if not timerexists 'heavy xbow spam'
            createtimer 'heavy xbow spam'
            settimer 'heavy xbow spam' 20000
        endif
        if timer 'swing timer' >= 6500
            #
        elseif timer 'swing timer' >= 6000 and timer 'heavy xbow spam' > 5000
            overhead '[Heavy Xbow Ready]' 55
            settimer 'heavy xbow spam' 0
        elseif timer 'swing timer' >= 4800 and timer 'hally spam' > 5000
            overhead '[Hally Ready]' 55
            settimer 'hally spam' 0
        elseif timer 'swing timer' >= 4300 and timer 'xbow spam' > 5000
            #overhead '[Xbow Ready]' 55
            settimer 'xbow spam' 0
        elseif timer 'swing timer' >= 3520 and timer 'bow spam' > 5000
            #overhead '[Bow Ready]' 55
            settimer 'bow spam' 0
        endif    
    endif
    
    
    
    # Target Lock Status
    if not timerexists 'target_lock_spam'
        createtimer 'target_lock_spam'
        settimer 'target_lock_spam' 20000
    endif
    
    if varexist 'target_lock'
        if timer 'target_lock_spam' > 5000
            @setvar! 'lastserial' lasttarget
            if 'lastserial' = 'target_lock'
                overhead '[Target Locked]' 44
            else
                overhead '[Target Unlocked]' 33
            endif
            settimer 'target_lock_spam' 0
        endif
    endif
        
    # Setup weapon to reequip
    if not varexist 'reequip_weapon_left'
        if findlayer self lefthand as 'reequip_weapon_left_serial'
            #overhead '[Setup Weapon Left]' 55
            @setvar! 'reequip_weapon_left' 'reequip_weapon_left_serial'
        endif
    endif
    
    if not varexist 'reequip_weapon_right'
        if findlayer self righthand as 'reequip_weapon_right_serial'
            #overhead '[Setup Weapon Right]' 55
            @setvar! 'reequip_weapon_right' 'reequip_weapon_right_serial'
        endif
    endif
    
    # Disarm Reequip Timer
    if insysmsg 'Their attack disarms you'
        overhead '[Disarmed!]' 33
        clearsysmsg 
    endif
    
    if insysmsg 'no longer disarmed'
        overhead '[Reequip Weapon]' 44
        if varexist 'reequip_weapon_left'
            lift 'reequip_weapon_left' 'backpack'
            drop self LeftHand      
        elseif varexist 'reequip_weapon_right'
            lift 'reequip_weapon_right'
            drop self RightHand
        endif
        clearsysmsg
    endif
    
    # Unset weapon so that its fresh each run.
    if varexist 'reequip_weapon_left'
        if findlayer self lefthand as 'reequip_weapon_left_serial'
            if 'reequip_weapon_left' = 'reequip_weapon_left_serial'
                # Wearing the setup weapon
            else
                # Wearing a new weapon, setup.
                #overhead '[Setup Weapon Left]' 55
                @setvar! 'reequip_weapon_left' 'reequip_weapon_left_serial'
            endif
        else
            if find 'reequip_weapon_left' 'backpack'
                # Weapon is in backpack.
            else
                # Unset weapon, not in backpack or wearing.
                #overhead '[Removed Weapon Left]' 33
                @unsetvar 'reequip_weapon_left'
            endif
        endif
    endif
    if varexist 'reequip_weapon_right'
        if findlayer self righthand as 'reequip_weapon_right_serial'
            if 'reequip_weapon_right' = 'reequip_weapon_right_serial'
                # Wearing the setup weapon
            else
                # Wearing a new weapon, setup.
                #overhead '[Setup Weapon Right]' 55
                @setvar! 'reequip_weapon_right' 'reequip_weapon_right_serial'
            endif
        else
            if find 'reequip_weapon_right' 'backpack'
                # Weapon is in backpack.
            else
                # Unset weapon, not in backpack or wearing.
                #overhead '[Removed Weapon Right]' 33
                @unsetvar 'reequip_weapon_right'
            endif
        endif
    endif
    
    # PVP Event Messages
    if insysmsg 'You have been credited with'
        overhead '[Kill Earned]' 33
        clearsysmsg 
    endif
    if insysmsg 'kill points earned'
        overhead '[KP Earned]' 423
        clearsysmsg 
    endif
    
    if not timerexists 'cp timer'
        createtimer 'cp timer'
        settimer 'cp timer' 30000
    endif
    
    if insysmsg 'control points earned'
        if insysmsg '10 control points earned'
            overhead '[10 CP Earned]' 423
        elseif insysmsg '15 control points earned'
            overhead '[15 CP Earned]' 423
        elseif insysmsg '20 control points earned'
            overhead '[20 CP Earned]' 423
        endif
        clearsysmsg
        settimer 'cp timer' 0
    endif
    
    if not timerexists 'cp spam'
        createtimer 'cp spam'
        settimer 'cp spam' 5000
    endif
    
    if timer 'cp timer' >= 31000
        #
    elseif timer 'cp timer' >= 30000 and timer 'cp spam' >= 5000
        overhead '[CP Tick Now]'
        settimer 'cp spam' 0
    elseif timer 'cp timer' >= 25000 and timer 'cp spam' >= 5000
        overhead '[CP Tick 5 Sec]'
        settimer 'cp spam' 0
    elseif timer 'cp timer' >= 20000 and timer 'cp spam' >= 5000
        overhead '[CP Tick 10 Sec]'
        settimer 'cp spam' 0
    endif
    
    # Hamstring
    if insysmsg 'will now attempt to hamstring'
        overhead '[Hamstring: On]' 68
        clearsysmsg
    elseif insysmsg 'refrain from making hamstring'
        overhead '[Hamstring: Off]' 33
        clearsysmsg
    elseif insysmsg 'fail to hamstring'
        overhead '[Hamstring Fail]' 33
        clearsysmsg
    elseif insysmsg 'your attack hamstrings your target'
        overhead '[Hamstrung Target]'
        clearsysmsg
    endif
    
    # Disarm
    if insysmsg 'will now attempt to disarm'
        overhead '[Disarm: On]' 68
        clearsysmsg
    elseif insysmsg 'refrain from making disarm'
        overhead '[Disarm: Off]' 33
        clearsysmsg
    elseif insysmsg 'fail to disarm'
        overhead '[Disarm Fail]' 33
        clearsysmsg 
    endif
    
    # Prevent Criminal Looting
    if insysmsg 'Criminal looting will now be allowed'
        overhead '[Allow Crim Loot]' 68
    elseif insysmsg 'Criminal looting will now be prevented'
        overhead '[Prevent Crim Loot]' 33
    endif
    
    # Prevent Criminal Healing
    if insysmsg 'Criminal healing will now be allowed'
        overhead '[Allow Crim Healing]' 68
    elseif insysmsg 'Criminal healing will now be prevented'
        overhead '[Prevent Crim Healing]' 33
    endif
    
    # Interrupt
    if insysmsg 'concentration is disturbed'
        overhead '[Interrupted]' 125
        clearsysmsg
    endif
    
    # Already Casting
    if insysmsg 'You are already casting'
        overhead '[Already Casting]' 88
        clearsysmsg
    endif
    
    # Ranover
    if insysmsg 'you shove them out of the way'
        overhead '[Ranover Somebody]' 44
        clearsysmsg
    endif
    
    # Ranover Invis
    if insysmsg 'you shove something invisible'
        overhead '[Ranover Invis]' 44
        clearsysmsg
    endif
    
    # Resist Poison
    if insysmsg 'You resist a poison effect'
        overhead '[Resist Poison]' 68
        clearsysmsg
    endif
    
    # Resist Disease
    if insysmsg 'You resist a disease effect'
        overhead '[Resist Disease]' 68
        clearsysmsg
    endif
    
    # Resist Bleed
    if insysmsg 'You resist a bleed effect'
        overhead '[Resist Bleed]' 68
        clearsysmsg
    endif
    
    # Stealth Refresh
    if insysmsg 'ready to continue stealthing'
        overhead '[Stealth Refresh]' 68
        clearsysmsg
    endif
    
    # Hinder Target
    if insysmsg 'hinders your target'
        overhead '[Hindered Target]' 68
        clearsysmsg
    endif
    
    # Mana Refund
    if insysmsg 'spellbook generates mana'
        overhead '[Mana Refund]' 125
        clearsysmsg
    endif
    
    # Charged Spell
    if insysmsg 'charge your spell'
        overhead '[Charged Spell]' 125
        clearsysmsg
    endif
    
    # Wizardry Hinder Ready
    if insysmsg 'cast a wizardry lightning spell again'
        overhead '[Lightning Hinder Ready]' 44
        clearsysmsg
    endif
    
    # Meditating
    if insysmsg 'enter a meditative trance'
        overhead '[Meditating]' 506
        clearsysmsg
    endif
    
    # Stop Meditating
    if insysmsg 'stop meditating'
        overhead '[Stop Meditating]' 33
        clearsysmsg
    endif
    
    # Monitor for free cure pot
    if insysmsg 'receive a free cure potion'
        settimer 'next free cure' 0
    endif
    
    ####################################################
    # Bandage Survival and non-revealing actions.
    ####################################################
    # Dex = Time to Heal
    # 25 = 15
    # 40 = 14
    # 55 = 13
    # 70 = 12
    # 85 = 11
    # 100 = 10
    if skill 'Healing' >= 1
        if not timerexists 'bandage time'
            createtimer 'bandage time'
            settimer 'bandage time' 20000
        endif

        if not timerexists 'aid cure sync'
            createtimer 'aid cure sync'
            settimer 'aid cure sync' 20000
        endif
    
        if insysmsg 'You begin applying the bandages'
            # If we manually applied a bandage, reset timer.
            settimer 'bandage time' 0
            clearsysmsg
        endif
        if dex <= 40
            # 15 second timer
            if hits < maxhits and timer 'bandage time' >= 15000         
                hotkey 'bandage self'
                settimer 'bandage time' 0
            endif
            # Attempt to last second cure before aid tick, regardless of free cut timer.
            if timer 'bandage time' < 15000 and timer 'bandage time' >= 14000
                if poisoned 'self' and timer 'potion recovery' > 1000
                    overhead '[Used: Cure Pot]' 68
                    hotkey 'Drink Cure'
                    # Set timer for free cure
                    if insysmsg 'receive a free cure potion'
                        settimer 'next free cure' 0
                    endif
                    settimer 'potion recovery' 0
                endif
            endif
        elseif dex < 55
            # 14 second timer
            if hits < maxhits and timer 'bandage time' >= 14000
                hotkey 'bandage self'
                settimer 'bandage time' 0
            endif
            # Attempt to last second cure before aid tick, regardless of free cut timer.
            if timer 'bandage time' < 14000 and timer 'bandage time' >= 13000
                if poisoned 'self' and timer 'potion recovery' > 1000
                    overhead '[Used: Cure Pot]' 68
                    hotkey 'Drink Cure'
                    # Set timer for free cure
                    if insysmsg 'receive a free cure potion'
                        settimer 'next free cure' 0
                    endif
                    settimer 'potion recovery' 0
                endif
            endif
        elseif dex < 70
            # 13 second timer
            if hits < maxhits and timer 'bandage time' >= 13000 
                hotkey 'bandage self'
                settimer 'bandage time' 0
            endif
            # Attempt to last second cure before aid tick, regardless of free cut timer.
            if timer 'bandage time' < 13000 and timer 'bandage time' >= 12000
                if poisoned 'self' and timer 'potion recovery' > 1000
                    overhead '[Used: Cure Pot]' 68
                    hotkey 'Drink Cure'
                    # Set timer for free cure
                    if insysmsg 'receive a free cure potion'
                        settimer 'next free cure' 0
                    endif
                    settimer 'potion recovery' 0
                endif
            endif
        elseif dex < 85
            # 12 second timer
            if hits < maxhits and timer 'bandage time' >= 12000 
                hotkey 'bandage self'
                settimer 'bandage time' 0
            endif
            # Attempt to last second cure before aid tick, regardless of free cut timer.
            if timer 'bandage time' < 12000 and timer 'bandage time' >= 11000
                if poisoned 'self' and timer 'potion recovery' > 1000
                    overhead '[Used: Cure Pot]' 68
                    hotkey 'Drink Cure'
                    # Set timer for free cure
                    if insysmsg 'receive a free cure potion'
                        settimer 'next free cure' 0
                    endif
                    settimer 'potion recovery' 0
                endif
            endif
        elseif dex < 100
            # 11 second timer
            if hits < maxhits and timer 'bandage time' >= 11000 
                hotkey 'bandage self'
                settimer 'bandage time' 0
            endif
            # Attempt to last second cure before aid tick, regardless of free cut timer.
            if timer 'bandage time' < 11000 and timer 'bandage time' >= 10000
                if poisoned 'self' and timer 'potion recovery' > 1000
                    overhead '[Used: Cure Pot]' 68
                    hotkey 'Drink Cure'
                    # Set timer for free cure
                    if insysmsg 'receive a free cure potion'
                        settimer 'next free cure' 0
                    endif
                    settimer 'potion recovery' 0
                endif
            endif
        else
            # 10 second timer
            if hits < maxhits and timer 'bandage time' >= 10000
                hotkey 'bandage self'
                settimer 'bandage time' 0
            endif
            # Attempt to last second cure before aid tick, regardless of free cut timer.
            if timer 'bandage time' < 10000 and timer 'bandage time' >= 9000
                if poisoned 'self' and timer 'potion recovery' > 1000
                    overhead '[Used: Cure Pot]' 68
                    hotkey 'Drink Cure'
                    # Set timer for free cure
                    if insysmsg 'receive a free cure potion'
                        settimer 'next free cure' 0
                    endif
                    settimer 'potion recovery' 0
                endif
            endif
        endif
    endif
    
    # If paralyzed, attempt to pouch.
    if not targetexists any
        if paralyzed
            if findtype 3705 'backpack' 38
                overhead '[Used: Pouch]' 33
                say [pouch
            endif
        endif
    endif
    
    # Food Buff
    # TODO
    
    ####################################################################
    # Hidden check, will avoid any actions below this marker if hidden.
    ####################################################################
    if hidden
        continue
    endif
    
    # Cure Survival
    if not targetexists any
        if poisoned 'self' and timer 'next free cure' >= 30000 and timer 'potion recovery' > 1000
            if findtype 3847 'backpack'
                overhead '[Used: Cure Pot]' 68
                hotkey 'Drink Cure'
                # Set timer for free cure
                if insysmsg 'receive a free cure potion'
                    settimer 'next free cure' 0
                endif
                settimer 'potion recovery' 0
            endif
        endif
    endif
    
    # Heal Potion
    if not timerexists 'healpot recovery'
        createtimer 'healpot recovery'
        settimer 'healpot recovery' 20000
    endif

    if hits < 55 and timer 'healpot recovery' >= 10000 and timer 'potion recovery' > 1000
        if targetexists 'beneficial'
            target 'self'
            wait 50
        endif
        if findtype 3852 'backpack'
            if hits < 45
                overhead '[Used: GHeal Pot]' 68
                hotkey 'Drink Heal'
                settimer 'healpot recovery' 0
                settimer 'potion recovery' 0
            endif
        endif
    endif 

endwhile

if dead
    overhead 'Stay Alive Failed!'
    stop
endif